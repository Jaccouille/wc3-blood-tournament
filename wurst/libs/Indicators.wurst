package Indicators

// Standard libs imports:
import ClosureTimers
import HashMap
import LinkedList

// Local imports:
import LocalObjectIDs

public interface Callback
    function run(unit target)

// A mapping of IDs from indicator units to actual units.
public let indicators = new IterableMap<int, int>()
    ..put('h009', UNIT_ARENA_MASTER)
    ..put('h008', UNIT_RACE_PICKER)


public let indicatorCallbacks = new IterableMap<int, LinkedList<Callback>>()

public function registerIndicator(int originID, int targetID)
    indicators.put(originID, targetID)

public function registerIndicatorCallback(int targetID, Callback cb)
    // Look up the list of callbacks for the given type.
    var list = indicatorCallbacks.get(targetID)

    // Build a default element, as necessary.
    if list == null
        // Create a new list.
        list = new LinkedList<Callback>()

        // Register the list for later user.
        indicatorCallbacks.put(targetID, list)

    // Register the callback.
    list.add(cb)

// It's easier to place units on editor and then replace them with our
// Wurst defined units
public function replaceIndicator()
    // Delay to ensure that all indicators are registered prior to execution.
    nullTimer() ->
        // Replace indicators one type at a time.
        indicators.forEach() (integer originID, integer targetID) ->
            // Find all matching indicators for the current type.
            ENUM_GROUP..clear().enumUnitsOfType(originID, null)

            // Replace each indicator with the corresponding unit.
            for origin in ENUM_GROUP
                // Extract the relevant information from the indicator.
                let owner = origin.getOwner()
                let pos   = origin.getPos()
                let angle = origin.getFacingAngle()

                // Remove the indicator once it is no longer needed.
                origin.remove()

                // Create the actual unit.
                let target = createUnit(owner, targetID, pos, angle)

                // Only show race picker, rest of the units are hidden
                // Only showed for debug purposes.
                if targetID != UNIT_RACE_PICKER and targetID != UNIT_ARENA_MASTER
                    target.hide()

                // TODO : for some reason this following code
                // may provoke crash due to HotkeyMapping definition in CommonUtils.wurst
                // Find out why
                // Look up the callbacks for this indicator.
                if indicatorCallbacks.has(targetID)
                    let list = indicatorCallbacks.get(targetID)

                    // Execute each callback, as necessary.
                    if list != null
                        list.forEach() (Callback cb) ->
                            cb.run(target)
        destroy indicators
