package UnitObjEditingExt

import ChannelAbilityPreset
import ErrorHandling

import GridHotkeyPosition
import TooltipsUtils

/** Configure this variable to `false` if you don't want the
	property names to have spaces. */
@configurable constant USE_PROPERTY_SPACING = true
@configurable constant ATTACK_UPGD_LVL = 10
@configurable constant DEFENSE_UPGD_LVL = 5

public interface TooltipGenerator
    function addProperty(string title, string value, string bonusValue, int lvls)
    function applyToDef(UnitOrHeroDefinition def)
    function getLastTooltip() returns string

public class UnitExt extends UnitDefinition
    TooltipGenerator tooltipGen = null
    boolean listen = false
    ChannelAbilityPreset toggleAbil = null

    construct(int newId, int origId, TooltipGenerator tgen)
        super(newId, origId)
        registerTooltipGenerator(tgen)

    function addTooltipProperty(string pName, string value, string bonus, int lvls)
        if tooltipGen != null and listen
            if USE_PROPERTY_SPACING
                tooltipGen.addProperty(pName, value, bonus, lvls)
            else
                tooltipGen.addProperty(pName.replace(" ", ""), value, bonus, lvls)

    function addTooltipProperty(string pName, int value, int bonus, int lvls)
        let bonusStr = bonus == 0 ? "" : bonus.toString()
        addTooltipProperty(pName, value.toString(), bonusStr, lvls)

    function addTooltipProperty(string pName, real value, int bonus, int lvls)
        var s = value.toString()
        s = s.endsWith(".0") ? s.substring(0, s.length() - 2) : s
        let bonusStr = bonus == 0 ? "" : bonus.toString()
        addTooltipProperty(pName, s, bonusStr, lvls)

    function registerTooltipGenerator(TooltipGenerator tgen)
        tooltipGen = tgen

    function tooltipStartListen()
        listen = true

    function tooltipStopListen()
        tooltipStopListen(true)

    function tooltipStopListen(boolean build)
        listen = false
        if tooltipGen != null and build
            tooltipGen.applyToDef(this)

    function setAttack1AttackTypeExt(AttackType data)
        let dataStr = data.toObjectString()
        def.setString("ua1t", dataStr)
        addTooltipProperty("Damage Type", dataStr.firstUpper(), "", 0)

    function setAttack1DamageBaseExt(int data)
        setAttack1DamageBaseExt(data, 0, 0)

    function setAttack1DamageBaseExt(int data, int bonus, int lvls)
        def.setInt("ua1b", data)
        def.setInt("udu1", bonus)
        // Setting base damage to 15 without random spread set the damage to  16
        // Add +1 to make sure the tooltip is accurate
        let dataAdjusted = data + 1
        addTooltipProperty("Damage", dataAdjusted, bonus, lvls)

    function setAttack1CooldownTimeExt(real data)
        setAttack1CooldownTimeExt(data, 0, 0)

    function setAttack1CooldownTimeExt(real data, int bonus, int lvls)
        def.setUnreal("ua1c", data)
        addTooltipProperty("Attack Speed", data, bonus, lvls)

    function setHitPointsMaximumBaseExt(int data)
        setHitPointsMaximumBaseExt(data, 0, 0)

    function setHitPointsMaximumBaseExt(int data, int bonus, int lvls)
        def.setInt("uhpm", data)
        addTooltipProperty("Health Point", data, bonus, lvls)

    function setDefenseBaseExt(int data)
        setDefenseBaseExt(data, 0, 0)

    function setDefenseBaseExt(int data, int bonus, int lvls)
        def.setInt("udef", data)
        def.setInt("udup", data)
        addTooltipProperty("Armor", data, bonus, lvls)

    function setArmorTypeExt(ArmorType data)
        let dataStr = data.toObjectString()
        def.setString("udty", dataStr)
        addTooltipProperty("Armor Type", dataStr.firstUpper(), "", 0)

    function setSpeedBaseExt(int data)
        setSpeedBaseExt(data, 0, 0)

    function setSpeedBaseExt(int data, int bonus, int lvls)
        def.setInt("umvs", data)
        addTooltipProperty("Movement Speed", data, bonus, lvls)

    function setAttack1RangeExt(int data)
        setAttack1RangeExt(data, 0, 0)

    function setAttack1RangeExt(int data, int bonus, int lvls)
        def.setInt("ua1r", data)
        addTooltipProperty("Attack Range", data, bonus, lvls)

    function setManaExt(int data)
        setManaExt(data, 0, 0)

    function setManaExt(int data, int bonus, int lvls)
        def.setInt("umpm", data)
        def.setInt("umpi", data)
        addTooltipProperty("Mana", data, bonus, lvls)

    function createToggleAbility(int id)
        this.toggleAbil = new ChannelAbilityPreset(id, 1, true)
            ..setDummyAbility()
            ..presetTargetTypes(Targettype.NONE)
            ..setCastingTime(1, 0)
            ..setTooltipNormalExtended(1, this.tooltipGen.getLastTooltip())

    function setNameExt(string hotkey, string name)
        this.setHotkeyEnhance(hotkey)
        this.setName(name)
        this.setTooltipBasic(makeToolTipNorm(hotkey, "Recruit " + name))

    function setIconExt(string path)
        this.setIconGameInterface(path)
        if this.toggleAbil != null
            this.toggleAbil.setIconNormal(path)

    function setHotkeyEnhance(string hotkey)
        if not hotKeyMapping.has(hotkey)
            error("Hotkey button position mapping has no \"{0}\" element".format(hotkey))
            return
        this.setHotkey(hotkey)
        this.setButtonPositionX(hotKeyMapping.get(hotkey).x)
        this.setButtonPositionY(hotKeyMapping.get(hotkey).y)

    function setRepairCommon(int repairTime)
        this.setRepairTime(repairTime)
        this.setRepairGoldCost(0)
        this.setRepairLumberCost(0)

    function setHitPointRegeBasic()
        this.setHitPointsRegenerationRate(0.250)
        this.setHitPointsRegenerationType("always")

    function setGoldData(int data)
        this.setGoldCost(data)
        this.setPointValue(data)
