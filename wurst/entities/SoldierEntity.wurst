package SoldierEntity

// Standard libs imports:
import HashMap
import LinkedList

// Local imports:
import IDListConstant
import PlayerData
import UnitEntity
import GameConstants

let typeIDs = new LinkedList<int>
    ..addAll(HUMAN_UNITS)
    ..addAll(ORC_UNITS)
    ..addAll(UNDEAD_UNITS)
    ..addAll(NE_UNITS)

interface LastArmyRemaining
    abstract function execute()

public LastArmyRemaining onLastArmyRemaining

interface AllUnitDead
    abstract function execute()

public AllUnitDead onAllUnitDead

public constant playerSoldiers = new HashMap<player, LinkedList<SoldierEntity>>

public class SoldierEntity extends UnitEntity
    construct(unit whichUnit)
        super(whichUnit)

        let soldierList = playerSoldiers.get(theUnit.getOwner())

        if soldierList == null
            playerSoldiers.put(theUnit.getOwner(), asList(this))
        else
            soldierList.add(this)

        EventListener.add(theUnit, EVENT_PLAYER_UNIT_DEATH, () -> begin
            let killer = EventData.getKillingUnit().getOwner()

            // Add blood points bounty
            if killer != ALTAR_PLAYER
                pData.get(killer).addBounty(theUnit.getPointValue())
            playerSoldiers.get(theUnit.getOwner()).remove(this)

            if playerSoldiers.get(theUnit.getOwner()).isEmpty()
                playerSoldiers.remove(theUnit.getOwner())

                Log.info("Removed " + theUnit.getOwner().getName())

                if playerSoldiers.size() == 1
                    onLastArmyRemaining.execute()
                if playerSoldiers.size() == 0
                    onAllUnitDead.execute()
        end)

    override function postCreate()
        super.postCreate()


init
    typeIDs.forEach() (int typeID) ->
        SoldierEntity.register(typeID, target -> new SoldierEntity(target))
