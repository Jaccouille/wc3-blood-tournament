package RaceSelection

// Standard libs imports:
import ClosureEvents
import Assets
import ClosureTimers

// Local imports:
import ColorUtils
import GameConfig
import GameStates
import IDListConstant
import LocalObjectIDs
import RoundController

function hasBuildings(player _p) returns boolean
    let pData = pDatas.get(_p)

    let grp = GetUnitsInRectMatching(
        pData.buildingRect,
        Filter( -> GetFilterUnit().isType(UNIT_TYPE_STRUCTURE) and GetFilterUnit().getTypeId() != UNIT_RACE_PICKER ))
    let size = grp.size()
    grp.close()
    return size > 0


init
    EventListener.add(EVENT_PLAYER_UNIT_TRAIN_CANCEL) ->
        if EventData.getTriggerUnit().getTypeId() == UNIT_RACE_PICKER
            SetPlayerTechResearchedSwap(UPGD_RACE_PICK, 1, EventData.getTriggerUnit().getOwner())

    EventListener.onCast(ABIL_RANDOM_RACE) (unit caster) ->
        let owner = caster.getOwner()
        let pData = pDatas.get(owner)

        if hasBuildings(owner)
            printTimedToPlayer("Cannot random with existing building".color(SPECIAL_COLOR), 5, owner)
        else
            if pData.builder != null
                SetPlayerTechMaxAllowed(owner, pData.builder.getTypeId(), 1)
                flashEffect(Abilities.aIilTarget, pData.builder.getPos3Real())
                pData.builder.remove()
            if not pData.racePicker.issueImmediateOrder(UnitId2String(BUILDER_LIST.getRandomElement()))
                nullTimer() ->
                    pData.racePicker.issueImmediateOrder(UnitId2String(BUILDER_LIST.getRandomElement()))

    EventListener.onCast(ABIL_REPICK) (unit caster) ->
        let owner = caster.getOwner()
        let pData = pDatas.get(owner)

        if hasBuildings(owner)
            printTimedToPlayer("Cannot repick with existing building".color(SPECIAL_COLOR), 5, owner)
        else if roundController.hasRoundStarted
            printTimedToPlayer("Cannot repick after first round".color(SPECIAL_COLOR), 5, owner)
        else if pData.builder == null
            printTimedToPlayer("No builder to repick".color(SPECIAL_COLOR), 5, owner)
        else
            SetPlayerTechMaxAllowed(owner, pData.builder.getTypeId(), 1)
            flashEffect(Abilities.aIilTarget, pData.builder.getPos3Real())
            pData.builder.remove()


    EventListener.add(EVENT_PLAYER_UNIT_TRAIN_FINISH) ->
        let u = EventData.getTrainedUnit()
        if u.isType(UNIT_TYPE_PEON)
            // Remove unit from building UI
            SetPlayerTechMaxAllowed(u.getOwner(), u.getTypeId(), 0)
            if gameConfig.isMultipleBuilderEnabled()
                u.setUseFood(false)

    GameStates.modesSelection.onExit() (state) ->
        pDatas.forEach() (player _p, PlayerData pData) ->
            SetPlayerTechResearchedSwap(UPGD_RACE_PICK, 1, _p)
            if gameConfig.isAllRandomEnabled()
                pData.builder = createUnit(_p, BUILDER_LIST.getRandomElement(), pData.racePicker.getPos())
                pData.racePicker.removeAbility(ABIL_REPICK)
                pData.racePicker.removeAbility(ABIL_RANDOM_RACE)
            else
                SetPlayerTechResearchedSwap(UPGD_ALL_RANDOM, 1, _p)

    OnRoundStart.add() (Round round) ->
        if not gameConfig.isMultipleBuilderEnabled()
            pDatas.forEach() (player _p, PlayerData pData) ->
                if pData.builder == null
                    pData.builder = createUnit(_p, BUILDER_LIST.getRandomElement(), pData.racePicker.getPos())
                SetPlayerTechResearchedSwap(UPGD_ALL_RANDOM, 0, _p)
                SetPlayerTechResearchedSwap(UPGD_RACE_PICK, 0, _p)
