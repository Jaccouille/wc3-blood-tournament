package BuilderAI

// Standard libs imports:
import ClosureTimers
import HashMap
import LinkedListModule

// Local imports:
import GameConfig
import GameStates
import IDListConstant
import LocalObjectIDs
import Players
import Round
import Toolkit
import TerrainUtils

let playerToBuilderAI = new HashMap<player, BuilderAI>







let BUILDER_OPTION = new HashMap<int, LinkedList<int>>
    ..put(UNIT_HUMAN_BUILDER, asList(
        BUILDING_FOOTMAN,
        BUILDING_ELVEN_ARCHER,
        BUILDING_KNIGHT,
        BUILDING_PRIEST,
        BUILDING_PALADIN,
        BUILDING_MAGE,
        BUILDING_ARCHMAGE,
        BUILDING_SWORD_MASTER,
        BUILDING_ELVEN_MAGE,
        BUILDING_RIFLEMAN
    ))
    ..put(UNIT_ORC_BUILDER, asList(
        BUILDING_GRUNT,
        BUILDING_HEADHUNTER,
        BUILDING_RAIDER,
        BUILDING_WITCH_DOCTOR,
        BUILDING_SHAMAN,
        BUILDING_OGRE,
        BUILDING_FARSEER,
        BUILDING_BLADE_MASTER,
        BUILDING_SHADOW_HUNTER,
        BUILDING_DEMOLISHER
    ))
    ..put(UNIT_UNDEAD_BUILDER, asList(
        BUILDING_ZOMBIE,
        BUILDING_SKELETAL_ARCHER,
        BUILDING_NECROMANCER,
        BUILDING_DEATH_KNIGHT,
        BUILDING_BANSHEE,
        BUILDING_ABOMINATION,
        BUILDING_VAMPIRE,
        BUILDING_CRYPT_LORD,
        BUILDING_ARCHIMONDE
    ))
    ..put(UNIT_NIGHT_ELF_BUILDER, asList(
        BUILDING_ARCHER,
        BUILDING_HUNTRESS,
        BUILDING_DRYAD,
        BUILDING_MOUNTAIN_GIANT,
        BUILDING_AVATAR_OF_VENGEANCE,
        BUILDING_DRUID_TALON,
        BUILDING_DRUID_CLAW,
        BUILDING_DAEMON_HUNTER,
        BUILDING_PRIESTESS_OF_THE_MOON,
        BUILDING_BALLISTA
    ))

let UPGRADE_OPTIONS = new HashMap<int, LinkedList<int>>
    ..put(BUILDING_FOOTMAN, asList(ITEM_CAPTAIN))
    ..put(BUILDING_ELVEN_ARCHER, asList(ITEM_RANGER))
    ..put(BUILDING_KNIGHT, asList(ITEM_CRUSADER))
    ..put(BUILDING_PRIEST, asList(ITEM_SPELL_BREAKER, ITEM_INNER_FIRE))
    ..put(BUILDING_MAGE, asList(ITEM_FIRE_MAGE, ITEM_ICE_MAGE, ITEM_DARK_MAGE))

    ..put(BUILDING_GRUNT, asList(ITEM_BERSERKER))
    ..put(BUILDING_HEADHUNTER, asList(BUILDING_HEADHUNTER_BERSERKER, BUILDING_TRAPPER))
    ..put(BUILDING_WITCH_DOCTOR, asList(ITEM_HEALING_WAVE))
    ..put(BUILDING_SHAMAN, asList(ITEM_SPIRIT_WOLF1))
    ..put(BUILDING_RAIDER, asList(ITEM_PACK_LEADER))
    ..put(BUILDING_OGRE, asList(ITEM_OGRE_ARMORED))

    ..put(BUILDING_ZOMBIE, asList(BUILDING_GHOUL))
    ..put(BUILDING_CRYPT_FIEND, asList(BUILDING_NERUBIAN))
    ..put(BUILDING_SKELETAL_ARCHER, asList(ITEM_FIRE_ARROW, ITEM_FROST_ARROW))
    ..put(BUILDING_NECROMANCER, asList(ITEM_LICH, ITEM_CRIPPLE, ITEM_RAISE_SKELETON_MAGE))
    ..put(BUILDING_BANSHEE, asList(ITEM_UNHOLY_FRENZY))
    ..put(BUILDING_VAMPIRE, asList(ITEM_VAMPIRE_LORD))

    ..put(BUILDING_DRYAD, asList(ITEM_KEEPER_OF_THE_GROVE))
    ..put(BUILDING_HUNTRESS, asList(ITEM_PRIESTESS_OF_THE_MOON))
    ..put(BUILDING_ARCHER, asList(BUILDING_NE_RANGER))
    ..put(BUILDING_DRUID_CLAW, asList(ITEM_BEAR, ITEM_OWLBEAR))
    ..put(BUILDING_DRUID_TALON, asList(ITEM_ARCHDRUID, ITEM_WAND_OF_ILLUSION))

class BuilderAI
    use LinkedListModule
    player _player
    PlayerData pData
    CallbackPeriodic cb
    boolean isEnabled = false
    boolean enableForHumanPlayer = false

    construct(player _player)
        this._player = _player
        playerToBuilderAI.put(_player, this)
        pData = pDatas.get(_player)

    function pickBuilder()
        if pData.builder == null
            pData.racePicker.issueImmediateOrder(UnitId2String(BUILDER_LIST.getRandomElement()))

    // TODO: learn how to do AI
    function startAI()
        isEnabled = true
        pickBuilder()

        cb = doPeriodically(2) (CallbackPeriodic cb) ->
            // Try to upgrade existing units
            if pData._p.getLumber() > 100
                let buildingList = pData.buildingList.copy()
                let upgradeOptions = buildingList
                    .lodashFilter(building -> UPGRADE_OPTIONS.has(building.getTypeId()))
                if upgradeOptions.size() > 0
                    let unitToUpgrade = upgradeOptions.getRandomElement()
                    let upgradeChoice = UPGRADE_OPTIONS.get(unitToUpgrade.getTypeId()).getRandomElement()
                    if OrderId2StringBJ(upgradeChoice)  != ""
                        unitToUpgrade.issueImmediateOrder(OrderId2StringBJ(upgradeChoice))
                    // Item behavior
                    // else
                    //     let itm = unitToUpgrade.addItemById(upgradeChoice)

            if pData.builder != null
                let unitToBuild = BUILDER_OPTION.get(pData.builder.getTypeId()).getRandomElement()
                var pos = pData.buildingRect.randomPoint()
                while pos.isTerrainPathable(PATHING_TYPE_BUILDABILITY)
                    pos = pData.buildingRect.randomPoint()
                BlzQueueBuildOrderById(pData.builder, unitToBuild, pos.x, pos.y)
            if not isEnabled
                destroy cb

    function stopAI()
        BlzUnitForceStopOrder(pData.builder, true)
        isEnabled = false

    function toggleAI()
        if isEnabled and enableForHumanPlayer
            stopAI()
            Log.info("stop builder AI for " + _player.getName())
        else if enableForHumanPlayer
            startAI()
            Log.info("start builder AI for " + _player.getName())

function startAI()
    Log.debug("Start builder AI")
    for aiInstance in BuilderAI
        // TODO: only enable if player isn't bot
        if aiInstance.enableForHumanPlayer
            aiInstance.startAI()

function stopAI()
    Log.debug("Stop builder AI")
    for aiInstance in BuilderAI
        aiInstance.stopAI()

function toggleAI()
    Log.debug("Toggle builder AI")
    for aiInstance in BuilderAI
        if not aiInstance._player.isIngame()
            aiInstance.toggleAI()


init
    GameStates.gameplay.onEnter() state ->
        // TODO: enable for computer by default
        if gameConfig.isBotAIEnabled()
            ALL_PLAYERS.forEach() (player _player) ->
                let aiInstance = new BuilderAI(_player)
                if not _player.isIngame()
                    aiInstance.startAI()
                    aiInstance.enableForHumanPlayer = true

            // TODO: gameconfig parameters
            OnRoundFinish.add() (Round round) ->
                if round instanceof NormalRound
                    startAI()

            OnRoundStart.add() (Round round) ->
                stopAI()

    registerToolkitCommand("ai") (player triggerPlayer, LinkedList<string> arguments) ->
        let aiInstance = playerToBuilderAI.get(triggerPlayer)
        aiInstance.enableForHumanPlayer = aiInstance.enableForHumanPlayer == false ? true : false
        playerToBuilderAI.get(triggerPlayer).toggleAI()
