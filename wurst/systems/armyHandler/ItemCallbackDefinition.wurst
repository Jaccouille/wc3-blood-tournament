package ItemCallbackDefinition

import HashMap
import LinkedList
import LocalObjectIDs

public let itemAddUnitMap = new HashMap<int, ItemAddUnit>
public let itemModifyUnitMap = new HashMap<int, ItemModifyUnit>

interface AddUnitCallbackListener
    function execute(LinkedList<int> unitTospawn, int count)

interface ActionCallbackListener
    function execute(unit u)

public class ItemAddUnit
    private AddUnitCallbackListener listener

    function addAction(AddUnitCallbackListener listener)
        this.listener = listener

    function execute(LinkedList<int> unitTospawn, int count)
        this.listener.execute(unitTospawn, count)

public class ItemModifyUnit
    private ActionCallbackListener listener

    function addAction(ActionCallbackListener listener)
        this.listener = listener

    function execute(unit u)
        this.listener.execute(u)


init
    let captainAction = new ItemAddUnit()
        ..addAction() (LinkedList<int> unitTospawn, int count) ->
        for i = 0 to count
            unitTospawn.push(UNIT_ROYAL_GUARD)
    itemAddUnitMap.put(ITEM_CAPTAIN, captainAction)

    let crusaderAction = new ItemAddUnit()
        ..addAction() (LinkedList<int> unitTospawn, int count) ->
        for i = 1 to count
            unitTospawn.replace(unitTospawn.getRandomElement(), UNIT_CRUSADER)

    let crusaderUnitAction = new ItemModifyUnit()
        ..addAction() (unit u) ->
        u.setScale(2.0)
    itemAddUnitMap.put(ITEM_CRUSADER, crusaderAction)
    itemModifyUnitMap.put(ITEM_CRUSADER, crusaderUnitAction)

    let rangerAction = new ItemAddUnit()
        ..addAction() (LinkedList<int> unitTospawn, int count) ->
        for i = 0 to count
            unitTospawn.push(UNIT_RANGER)
    itemAddUnitMap.put(ITEM_RANGER, rangerAction)

    let spellBreakerAction = new ItemAddUnit()
        ..addAction() (LinkedList<int> unitTospawn, int count) ->
        for i = 0 to count
            unitTospawn.push(UNIT_SPELL_BREAKER)
    itemAddUnitMap.put(ITEM_SPELL_BREAKER, spellBreakerAction)


    let innerFireAction = new ItemModifyUnit()
        ..addAction() (unit u) ->
        u.addAbility(ABIL_INNER_FIRE)
        u.issueImmediateOrder("innerfireon")

    itemModifyUnitMap.put(ITEM_INNER_FIRE, innerFireAction)


    let darkMageAction = new ItemModifyUnit()
        ..addAction() (unit u) ->
        u.addAbility(ABIL_ORB_DARKNESS)

    itemModifyUnitMap.put(ITEM_DARK_MAGE, darkMageAction)


    let fireMageAction = new ItemModifyUnit()
        ..addAction() (unit u) ->
        u.addAbility(ABIL_INCINERATE)
        u.addAbility(ABIL_AUTOCAST_FIRE_SHIELD)
        u.issueImmediateOrder("frostarmoron")

    itemModifyUnitMap.put(ITEM_FIRE_MAGE, fireMageAction)


    let iceMageAction = new ItemModifyUnit()
        ..addAction() (unit u) ->
        u.addAbility(ABIL_ORB_FROST)
        u.addAbility(ABIL_FROST_ARMOR)
        u.issueImmediateOrder("frostarmoron")

    itemModifyUnitMap.put(ITEM_ICE_MAGE, iceMageAction)
