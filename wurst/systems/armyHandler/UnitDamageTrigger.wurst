package UnitDamageTrigger

// Standard libs imports:
import BuffIds
import ClosureTimers
import DamageEvent
import OrderIds
import InstantDummyCaster

// Local imports:
import LocalObjectIDs
import PlayerData
import PlayerExtension


init
    DamageEvent.addListener() ->
        let target = DamageEvent.getTarget()
        let src = DamageEvent.getSource()

        if src.hasAbility(ABIL_ABOLISH_MAGIC) and src.getAbilityCooldownRemaining(ABIL_ABOLISH_MAGIC) <= 0
            and target.isType(UNIT_TYPE_SUMMONED)
            src.issueTargetOrderById(OrderIds.autodispel, target)


        switch target.getTypeId()
            case UNIT_WARDEN
                if target.getPos().distanceTo(src.getPos()) > 200
                    BlzQueuePointOrderById(target, OrderIds.blink, src.getPos().x, src.getPos().y)
                    doAfter(0.9) ->
                        BlzQueueImmediateOrderById(target, OrderIds.fanofknives)

        switch src.getTypeId()
            case UNIT_SHAMAN
                if src.getAbilityCooldownRemaining(ABIL_SUMMON_SPIRIT_WOLF1) <= 0
                    src.issueImmediateOrderById(OrderIds.spiritwolf)
            case UNIT_FARSEER
                if src.getAbilityCooldownRemaining(ABIL_SUMMON_SPIRIT_WOLF2) <= 0
                    src.issueImmediateOrderById(OrderIds.spiritwolf)
            case UNIT_COURT_ENCHANTRESS
                src.issueImmediateOrderById(OrderIds.parasiteon)
            case UNIT_OWLBEAR
                if not target.hasAbility(BuffIds.howlofTerror)
                    src.issueImmediateOrderById(OrderIds.howlofterror)
            case UNIT_SIREN
                if src.hasAbility(ABIL_CHARM)
                    if GetRandomInt(0, 100) <= 10
                        // Log.info("Removing " + target.getName())
                        // if not pDatas.get(target.getOwner().getTruePlayerFromDummy()).spawnedUnits.remove(target)
                            // Log.info("merde")
                        InstantDummyCaster.castTarget(
                            src.getOwner(), ABIL_CHARM, 1, OrderIds.charm, target)
                        pDatas.get(src.getOwner().getTruePlayerFromDummy()).spawnedUnits.add(target)

            case UNIT_SPIRIT_WALKER
                if src.hasAbility(ABIL_ANCESTRAL_SPIRIT)
                    if GetRandomInt(0, 100) <= 10
                        src.issueImmediateOrderById(OrderIds.ancestralspirit)
