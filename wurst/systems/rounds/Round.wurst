package Round

// Standard libs imports:
import ClosureForGroups
import ClosureTimers
import public ClosureKeyPresses
import public LinkedList
import LocalObjectIDs
import MapBounds

// Third party imports:
import public Lodash

// Local imports:
import ArmySpawner
import OrderBlocker
import ScoreBoard
import SpawnZone
import public GameConstants
import public PlayerData
import ColorUtils

// Distance between units positioned in formation mode
@configurable constant FORMATION_WIDTH = 120.
@configurable constant FORMATION_HEIGHT = 80.

// Max number of unit per line
@configurable constant UNIT_PER_LINE = 4

@configurable constant UNIT_CAP = 80


interface LastArmyRemaining
    abstract function execute()

public LastArmyRemaining onLastArmyRemaining

interface AllUnitDead
    abstract function execute(player winner)

public AllUnitDead onAllUnitDead

public abstract class OnRoundFinish
    static constant listenerList = new LinkedList<OnRoundFinish>

    abstract function fire(Round round)

    static function add(OnRoundFinish listener)
        listenerList.add(listener)

    static function execute(Round round)
        for listener in listenerList
            listener.fire(round)

public abstract class OnRoundStart
    static constant listenerList = new LinkedList<OnRoundStart>

    abstract function fire(Round round)

    static function add(OnRoundStart listener)
        listenerList.add(listener)

    static function execute(Round round)
        for listener in listenerList
            listener.fire(round)


public abstract class Round
    LinkedList<player> roundParticipant = new LinkedList<player>
    int goldReward = 0
    int bloodPointReward = 0
    constant matchUp = new LinkedList<Pair<int, int>>
    player winner
    string roundName
    boolean hasStarted = false

    function setName(string name)
        this.roundName = name

    function addMatchup(Pair<int, int> matchUp)
        this.matchUp.add(matchUp)
        this.roundParticipant.add(players[matchUp.a], players[matchUp.b])

    function getOpponent(player _p) returns player
        player opponent = null
        for pair in matchUp
            if players[pair.a] == _p
                opponent = players[pair.b]
            else if players[pair.b] == _p
                opponent = players[pair.a]
        return opponent

    function start()
        // Round start
        // Log.debug("exec round callback")
        OnRoundStart.execute(this)
        // Log.debug(this.roundName + " start")
        doBeforeSpawn()
        // Log.debug(this.roundName + " doBeforeSpawn")
        spawnArmies()
        // Log.debug(this.roundName + " spawnArmies")
        doAfterSpawn()
        // Log.debug(this.roundName + " doAfterSpawn")

    function doBeforeSpawn()
        forUnitsAll() (unit u) ->
            if IsUnitDeadBJ(u)
                u.remove()
        hasStarted = true

    function doAfterSpawn()
        doAfter(2) ->
            forUnitsAll() (unit u) ->
                if not u.isType(UNIT_TYPE_STRUCTURE)
                    and not u.isType(UNIT_TYPE_PEON)
                    and u.getOwner() != ALTAR_PLAYER
                    u.unpause()
                    u.issuePointOrder("attack", CENTER)
            nullTimer() ->
                preventOrder = true
                orderBlocker.enable()

    function setRewards(int gold, int bloodPoint)
        this.goldReward = gold
        this.bloodPointReward = bloodPoint

    function finish(player winner)
        this.winner = winner
        hasStarted = false
        OnRoundFinish.execute(this)

    abstract function spawnArmies()
    abstract function spawnPortals()

    // Todo: rename
    function spawnArmy(SpawnZone spawnZone, LinkedList<unit> buidingList, player _p)
        if roundParticipant.has(_p) == false
            roundParticipant.add(_p)

        playerToArmySpawner.get(_p)
            ..setSpawnZone(spawnZone)
            ..setBuildingList(buidingList)
            ..spawnArmy()

    function computeCameraPos(SpawnZone spawnZone1, SpawnZone spawnZone2) returns vec2
        let center1 =  spawnZone1.spawnRect.getCenter()
        let center2 =  spawnZone2.spawnRect.getCenter()
        let direction =  center1.angleTo(center2)
        let dist = center1.distanceTo(center2) / 2
        return center1.polarOffset(direction, dist)

    function setupCamera(SpawnZone spawnZone1, SpawnZone spawnZone2, player p1, player p2)
        p1.panCameraToTimed(computeCameraPos(spawnZone1, spawnZone2), 0)
        p2.panCameraToTimed(computeCameraPos(spawnZone1, spawnZone2), 0)


public class NormalRound extends Round
    static int counter = 0

    construct()
        counter++
        this.roundName = "Round " + counter.toString()

    override function spawnPortals()
        SetDoodadAnimationRect(boundRect, DOODAD_PORTAL, "show", false)
        SetDoodadAnimationRect(boundRect, DOODAD_PORTAL, "birth", false)
        forUnitsAll() (unit u) ->
            if u.isType(UNIT_TYPE_STRUCTURE)
                u.setAnimation("Stand Victory")

    override function spawnArmies()
        var zoneIdx = 0
        for contestantPair in matchUp
            let p1 = players[contestantPair.a]
            let p2 = players[contestantPair.b]

            setupCamera(spawnZones.get(zoneIdx), spawnZones.get(zoneIdx + 1), p1, p2)
            spawnArmy(spawnZones.get(zoneIdx), pDatas.get(p1).buildingList, p1)
            spawnArmy(spawnZones.get(zoneIdx + 1), pDatas.get(p2).buildingList, p2)
            zoneIdx += 2

constant FINAL_ROUND_WEST_ZONE_IDX = 8
constant FINAL_ROUND_EAST_ZONE_IDX = 9

public class FinalRound extends Round
    static int counter = 0
    player looser = null

    construct()
        counter++
        // TODO: Dynamically handle following player count?
        if counter < 5
            this.roundName = "Quarter Final"
        else if counter < 7
            this.roundName = "Demi Final"
        else
            this.roundName = "Final"

    override function spawnPortals()
        SetDoodadAnimationRect(boundRect, DOODAD_ELIM_PORTAL, "show", false)
        SetDoodadAnimationRect(boundRect, DOODAD_ELIM_PORTAL, "birth", false)

    override function doAfterSpawn()
        var counter = 5
        orderBlocker.enable()
        doPeriodicallyCounted(1, counter + 1) (CallbackCounted cb) ->
            if cb.isLast()
                printTimed("FIGHT!!!".color(SPECIAL_COLOR), 3)
                forUnitsAll() (unit u) ->
                    if not u.isType(UNIT_TYPE_STRUCTURE)
                        and not u.isType(UNIT_TYPE_PEON)
                        and u.getOwner() != ALTAR_PLAYER
                        u.unpause()
                        u.issuePointOrder("attack", CENTER)
            else
                printTimed(counter.toString() + "...", 5)
            counter -= 1

    override function spawnArmies()
        for contestantPair in matchUp
            let p1 = players[contestantPair.a]
            let p2 = players[contestantPair.b]

            let msg = p1.getNameColored() + " VS " + p2.getNameColored()
            printTimed(msg, 5)

            setupCamera(spawnZones.get(FINAL_ROUND_WEST_ZONE_IDX), spawnZones.get(FINAL_ROUND_EAST_ZONE_IDX), p1, p2)
            spawnArmy(spawnZones.get(FINAL_ROUND_WEST_ZONE_IDX), pDatas.get(p1).buildingList, p1)
            spawnArmy(spawnZones.get(FINAL_ROUND_EAST_ZONE_IDX), pDatas.get(p2).buildingList, p2)

            // let msg = "{0} {1} units VS {2} {3} units"
            //     .format(
            //         p1.getNameColored(true), pDatas.get(p1).spawnedUnits.size().toString(),
            //         p2.getNameColored(true), pDatas.get(p2).spawnedUnits.size().toString()
            //     )
            // Log.debug(msg)
