package VotesDefinition

// Standard libs imports:
import HashMap
import MapBounds

// Local imports:
import ChatCommands
import GameConfig
import GameStates
import Players
import RoundController
import Vote
import BuilderAI

public function voteReady(player triggerPlayer)
    if roundController.currentRound.hasStarted
        printTimedToPlayer("Vote failed! Round already started!", 3, triggerPlayer)
    else
        if Vote.voteInstanceMap.has("Start round")
            Vote.voteInstanceMap.get("Start round").castVote(triggerPlayer)
        else
            var count = 0
            for _p in ALL_PLAYERS
                if _p.isHuman() and not playerToBuilderAI.get(_p).isEnabled
                    count++
            let voteInstance = new Vote("Start round", 60., count)
            voteInstance.addAction() () ->
                if not roundController.currentRound.hasStarted
                    if gameConfig.isDisplayPortalEnabled()
                        roundController.currentRound.spawnPortals()
                    roundController.startRound()
                else
                    printTimed("Vote failed! Round already started!", 3)
            voteInstance.castVote(triggerPlayer)


init
    GameStates.gameplay.onEnter() (state) ->
        registerCommandAll("r") (player triggerPlayer, LinkedList<string> arguments) ->
            voteReady(triggerPlayer)
        registerCommandAll("ready") (player triggerPlayer, LinkedList<string> arguments) ->
            voteReady(triggerPlayer)

        OnRoundStart.add() (Round round) ->
            if Vote.voteInstanceMap.has("Start round")
                Vote.voteInstanceMap.get("Start round").endVote(false)
